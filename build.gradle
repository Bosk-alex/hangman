defaultTasks 'clean', 'test'

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'scala'

sourceSets {
  main {
    java {srcDir 'src'}
    resources {srcDir 'src'}
  }
  test {
    java {srcDir 'test'}
    resources {srcDir 'test'}
    scala { srcDirs 'test' }
  }
}

repositories{
  mavenCentral()
//  maven {
//      url "https://oss.sonatype.org/content/repositories/snapshots/"
//  }
}

task libs(type: Sync) {
  from configurations.compile
  from configurations.providedCompile
  from configurations.runtime
  from configurations.providedRuntime
  from configurations.testCompile
  into "$buildDir/lib"
}

classes.dependsOn libs

[compileJava, compileTestJava]*.options.collect {options -> options.encoding = 'UTF-8'}
[compileJava, compileTestJava]*.options.collect {options -> options.debug = true}

test {
  include 'ee/era/hangman/**'
}

task uitest(type: Test) {
  systemProperties['browser'] = 'firefox'
}

task ie(type: Test) {
  systemProperties['browser'] = 'ie'
  systemProperties['timeout'] = '12000'
}

task htmlunit(type: Test) {
  systemProperties['browser'] = 'htmlunit'
}

task chrome(type: Test) {
  systemProperties['browser'] = 'chrome'
  systemProperties['webdriver.chrome.driver'] = '/usr/bin/chromedriver'
}

task phantomjs(type: Test) {
  systemProperties['browser'] = 'phantomjs'
}

tasks.withType(Test).all { testTask ->
  testTask.systemProperties['file.encoding'] = 'UTF-8'
  testTask.testLogging.showStandardStreams = true
  testTask.outputs.upToDateWhen { false }
}

dependencies {
  providedCompile 'org.eclipse.jetty:jetty-server:8.1.14.v20131031'
  providedCompile 'org.eclipse.jetty:jetty-webapp:8.1.14.v20131031'
  providedCompile 'org.eclipse.jetty:jetty-jsp:8.1.14.v20131031'
  providedCompile 'org.eclipse.jetty.orbit:javax.servlet:3.0.0.v20120910@jar'
  compile 'org.apache.struts:struts2-core:2.3.16'
  compile 'org.apache.struts.xwork:xwork-core:2.3.16'
  compile 'org.apache.struts:struts2-convention-plugin:2.3.16'
  compile 'org.apache.struts:struts2-json-plugin:2.3.16'
  compile 'org.liquibase:liquibase-core:3.1.1'
  compile 'com.google.inject.extensions:guice-struts2:3.0'
  runtime 'com.h2database:h2:1.3.175'
  compile 'c3p0:c3p0:0.9.1.2'
  compile 'org.slf4j:slf4j-log4j12:1.7.6'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  testCompile 'junit:junit:4.11'
  testCompile 'org.mockito:mockito-all:1.9.5'
  testCompile 'com.codeborne:selenide:2.8.1'
  testCompile 'com.opera:operadriver:1.5'
  testCompile 'org.scala-lang:scala-library:2.10.3'
  testCompile 'org.scalatest:scalatest_2.10:2.0'
  testCompile group: 'com.github.detro.ghostdriver', name: 'phantomjsdriver', version: '1.1.0', transitive: false
}

task(run, dependsOn: 'compile', type: JavaExec) {
  main = 'ee.era.hangman.Launcher'
  classpath = sourceSets.main.runtimeClasspath
  systemProperty 'file.encoding', 'UTF-8'
}

war {
  from 'webapp'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
