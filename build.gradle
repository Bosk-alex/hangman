apply plugin: 'java'
apply plugin: 'war'

repositories{
  mavenCentral()
  maven {
      url "https://oss.sonatype.org/content/repositories/snapshots/"
  }
}

dependencies {
  providedCompile fileTree(dir: 'lib-jetty', include: '*.jar')
  providedCompile 'jetty:javax.servlet:5.1.12'
  compile 'org.apache.struts:struts2-core:2.3.4.1'
  compile 'org.apache.struts.xwork:xwork-core:2.3.4.1'
  compile 'org.apache.struts:struts2-convention-plugin:2.3.4.1'
  compile 'org.apache.struts:struts2-json-plugin:2.3.4.1'
  compile 'org.slf4j:slf4j-log4j12:1.7.2'
  testCompile 'junit:junit:4.9'
  testCompile 'org.hamcrest:hamcrest-all:1.1'
  testCompile 'com.codeborne:selenide:1.8-SNAPSHOT'
  testCompile 'com.opera:operadriver:0.18'
}

task libs(type: Sync) {
  from configurations.compile
  from configurations.providedCompile
  from configurations.runtime
  from configurations.providedRuntime
  into "$buildDir/lib"
}

task testLibs(type: Sync) {
  from configurations.testCompile
  into "$buildDir/lib-test"
}

[compileJava, compileTestJava]*.options.collect {options -> options.encoding = 'UTF-8'}
[compileJava, compileTestJava]*.options.collect {options -> options.debug = true}

test {
  systemProperties 'file.encoding': 'UTF-8'
  exclude 'ee/era/hangman/ee/uitest/**/*.*'
}

task uitest(type: Test) {
  systemProperties 'file.encoding': 'UTF-8'
  systemProperties 'browser': 'chrome'
  systemProperties 'webdriver.chrome.driver': '/usr/bin/chromedriver'
  include 'ee/era/hangman/ee/uitest/**/*.*'
}

task(run, dependsOn: 'compile', type: JavaExec) {
  main = 'ee.era.hangman.Launcher'
  classpath = sourceSets.main.runtimeClasspath
  systemProperty 'file.encoding', 'UTF-8'
}

defaultTasks 'clean', 'libs', 'testLibs', 'test', 'war'

