defaultTasks 'clean', 'libs', 'test'

apply plugin: 'java'
apply plugin: 'war'

sourceSets {
  main {
    java {srcDir 'src'}
    resources {srcDir 'src'}
  }
  test {
    java {srcDir 'test'}
    resources {srcDir 'test'}
  }
}

repositories{
  mavenCentral()
//  maven {
//      url "https://oss.sonatype.org/content/repositories/snapshots/"
//  }
}

task libs(type: Sync) {
  from configurations.compile
  from configurations.providedCompile
  from configurations.runtime
  from configurations.providedRuntime
  from configurations.testCompile
  into "$buildDir/lib"
}

[compileJava, compileTestJava]*.options.collect {options -> options.encoding = 'UTF-8'}
[compileJava, compileTestJava]*.options.collect {options -> options.debug = true}

test {
  systemProperties 'file.encoding': 'UTF-8'
  testLogging.showStandardStreams = true
  include 'ee/era/hangman/**'
}

task uitest(type: Test) {
  systemProperties 'file.encoding': 'UTF-8'
  testLogging.showStandardStreams = true
}

task ie(type: Test) {
  systemProperties['browser'] = 'ie'
  systemProperties['timeout'] = '12000'
  systemProperties 'file.encoding': 'UTF-8'
  testLogging.showStandardStreams = true
}

task htmlunit(type: Test) {
  systemProperties['browser'] = 'htmlunit'
  systemProperties 'file.encoding': 'UTF-8'
  testLogging.showStandardStreams = true
}

task chrome(type: Test) {
  systemProperties['browser'] = 'chrome'
  systemProperties['webdriver.chrome.driver'] = '/usr/bin/chromedriver'
  systemProperties 'file.encoding': 'UTF-8'
  testLogging.showStandardStreams = true
}

task opera(type: Test) {
  systemProperties['browser'] = 'opera'
  systemProperties['timeout'] = '20000'
  systemProperties 'file.encoding': 'UTF-8'
  testLogging.showStandardStreams = true
}

dependencies {
  providedCompile 'org.eclipse.jetty:jetty-server:8.1.7.v20120910'
  providedCompile 'org.eclipse.jetty:jetty-webapp:8.1.7.v20120910'
  providedCompile 'org.eclipse.jetty:jetty-jsp:8.1.7.v20120910'
  providedCompile 'org.eclipse.jetty.orbit:javax.servlet:3.0.0.v20120910@jar'
  compile 'org.apache.struts:struts2-core:2.3.4.1'
  compile 'org.apache.struts.xwork:xwork-core:2.3.4.1'
  compile 'org.apache.struts:struts2-convention-plugin:2.3.4.1'
  compile 'org.apache.struts:struts2-json-plugin:2.3.4.1'
  compile 'org.slf4j:slf4j-log4j12:1.7.2'
  testCompile 'junit:junit:4.11'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  testCompile 'com.codeborne:selenide:2.4'
  testCompile 'com.opera:operadriver:1.5'
}

task(run, dependsOn: 'compile', type: JavaExec) {
  main = 'ee.era.hangman.Launcher'
  classpath = sourceSets.main.runtimeClasspath
  systemProperty 'file.encoding', 'UTF-8'
}

war {
  from 'webapp'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.7'
}
